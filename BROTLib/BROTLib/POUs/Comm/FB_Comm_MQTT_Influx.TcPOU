<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_Comm_MQTT_Influx" Id="{48590b1c-8149-4a75-a4a7-086285ed3fbf}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Comm_MQTT_Influx EXTENDS FB_Comm_MQTT
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	RaDec		: ST_RaDecCoordinate := (RightAscension := GVL_Math.LREAL_MIN, Declination := GVL_Math.LREAL_MIN);
	AltAz		: ST_AltAzCoordinate := (Altitude := GVL_Math.LREAL_MIN, Azimuth := GVL_Math.LREAL_MIN);
	sent		: INT := 0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[SUPER^();]]></ST>
    </Implementation>
    <Method Name="_handleMQTTMessage" Id="{6b8170c3-1db8-4027-aca0-99823e943d46}">
      <Declaration><![CDATA[METHOD _handleMQTTMessage : BOOL
VAR_INPUT
	sPayloadRcv		: STRING(255);
END_VAR
VAR
	measurement		: STRING(255);
	parameter		: STRING(255);
	value			: STRING(255);
	influxMessage	: FB_InfluxMessage;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[influxMessage(  sPayload := sPayloadRcv, measurement => measurement, parameter => parameter, value => value);
IF measurement = 'command' THEN
	IF parameter = 'power' AND Telescope <> 0 THEN
		IF STRING_TO_BOOL(value) THEN
			Telescope.Init();
		END_IF
	ELSIF parameter = 'park' AND Telescope <> 0 THEN
		Telescope.Park();
	ELSIF parameter = 'gohome' AND Telescope <> 0 THEN
		Telescope.Home();
	ELSIF parameter = 'track' AND Telescope <> 0 THEN
		IF RaDec.RightAscension > GVL_Math.LREAL_MIN AND RaDec.Declination > GVL_Math.LREAL_MIN THEN
			Telescope.TrackRaDec(RaDec);
		END_IF
		RaDec.RightAscension := GVL_Math.LREAL_MIN;
		RaDec.Declination := GVL_Math.LREAL_MIN;
	ELSIF parameter = 'goto' AND Telescope <> 0 THEN
		IF RaDec.RightAscension > GVL_Math.LREAL_MIN AND RaDec.Declination > GVL_Math.LREAL_MIN THEN
			Telescope.MoveRaDec(RaDec);
		END_IF
		RaDec.RightAscension := GVL_Math.LREAL_MIN;
		RaDec.Declination := GVL_Math.LREAL_MIN;		
	ELSIF parameter = 'slew' AND Telescope <> 0 THEN
		IF AltAz.Azimuth > GVL_Math.LREAL_MIN AND AltAz.Altitude > GVL_Math.LREAL_MIN THEN
			Telescope.MoveAltAz(AltAz);
		END_IF
		AltAz.Azimuth := GVL_Math.LREAL_MIN;
		AltAz.Altitude := GVL_Math.LREAL_MIN;	
	ELSIF parameter = 'stop' AND Telescope <> 0 THEN
		Telescope.Stop();
	ELSIF parameter = 'reset' AND Telescope <> 0 THEN
		Telescope.Reset();
	ELSIF parameter = 'nasmyth' AND Nasmyth <> 0 THEN
		Nasmyth.SetNasmyth(STRING_TO_USINT(value));
	ELSIF parameter = 'rightascension' THEN
		RaDec.RightAscension := STRING_TO_LREAL(value);
	ELSIF parameter = 'declination' THEN
		RaDec.Declination := STRING_TO_LREAL(value);
	ELSIF parameter = 'elevation' THEN
		AltAz.Altitude := STRING_TO_LREAL(value);
	ELSIF parameter = 'azimuth' THEN
		AltAz.Azimuth := STRING_TO_LREAL(value);
	ELSIF parameter = 'focus' AND Focus <> 0 THEN
		Focus.Position := STRING_TO_LREAL(value);
	ELSIF parameter = 'dome_open' AND Dome <> 0 THEN
		Dome.Open();
	ELSIF parameter = 'dome_close' AND Dome <> 0 THEN
		Dome.Close();
	ELSIF parameter = 'dome_park' AND Dome <> 0 THEN
		Dome.Park();
	ELSIF parameter = 'dome_track' AND Dome <> 0 THEN
		Dome.Track();
	ELSIF parameter = 'dome_stoptrack' AND Dome <> 0 THEN
		Dome.StopTrack();
	ELSIF parameter = 'dome_reset' AND Dome <> 0 THEN
		Dome.Reset();
	ELSIF parameter = 'filter' AND Filter <> 0 THEN
		Filter.SetFilter(STRING_TO_USINT(value));
	ELSIF parameter = 'derotator' AND Telescope <> 0 THEN
		Telescope.SetDerotatorOffset(STRING_TO_LREAL(value));
	ELSIF parameter = 'elevationoffset' AND Telescope <> 0 THEN
		Telescope.SetAltitudeOffset(STRING_TO_LREAL(value));
	ELSIF parameter = 'azimuthoffset' AND Telescope <> 0 THEN
		Telescope.SetAzimuthOffset(STRING_TO_LREAL(value));
	ELSIF parameter = 'derotatoroffset' AND Telescope <> 0 THEN
		Telescope.SetDerotatorOffset(STRING_TO_LREAL(value)); 
	ELSE
		ADSLOGSTR( ADSLOG_MSGTYPE_WARN OR ADSLOG_MSGTYPE_LOG, 'MQTT not understood %s', sPayloadRcv);
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Publish" Id="{0b9304dc-7cd5-4e61-ac34-34dd3afe6924}">
      <Declaration><![CDATA[METHOD Publish : BOOL
VAR_INPUT
	domain			: STRING(255) := 'environment';
	location		: STRING(255) := 'roof';
	parameter		: STRING(255);
	value			: STRING(255);
END_VAR
VAR_OUTPUT
	published	: BOOL;
END_VAR
VAR
	sPayloadPub		: STRING(255) := '';
	fbFormat		: FB_FormatString;
	bError			: BOOL;
	nErrID			: UDINT;
	retain_message	: BOOL := FALSE;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[//sPayloadPub := 'environment,location=roof,host=CX-4E6032 ';
fbFormat( 
	sFormat 	:= '%s,location=%s,host=%s %s=%s', 
	arg1 		:= F_STRING(domain), 
	arg2 		:= F_STRING(location), 
	arg3 		:= F_STRING(HostName),
	arg4 		:= F_STRING(parameter),
	arg5 		:= F_STRING(value),
	sOut 		=> sPayloadPub,
	bError 		=> bError, 
	nErrID 		=> nErrID 
);

// retain message if value is boolean
retain_message := value = 'TRUE' OR value = 'FALSE';

published := fbMqttClient.Publish(	
	sTopic			:= sTopicPublish, 
	pPayload		:= ADR(sPayloadPub), 
	nPayloadSize	:= LEN2(ADR(sPayloadPub)),
	eQoS			:= TcIotMqttQos.AtMostOnceDelivery,
	bRetain			:= retain_message, 
	bQueue			:= FALSE 
);

sent := sent + 1;]]></ST>
      </Implementation>
    </Method>
    <Method Name="PublishLog" Id="{dd46de85-b82a-4d9d-9238-d6c509f88651}">
      <Declaration><![CDATA[METHOD PublishLog : BOOL
VAR_INPUT
	level		: STRING(255) := 'INFO';
	message		: STRING(255);
END_VAR
VAR
	sPayloadPub	: STRING(255) := '';
	fbFormat	: FB_FormatString;
	bError		: BOOL;
	nErrID		: UDINT;
END_VAR
VAR_OUTPUT
	published	: BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[//sPayloadPub := 'environment,location=roof,host=CX-4E6032 ';
fbFormat( 
	sFormat := 'log,host=%s,level=%s message="%s"', 
	arg1 	:= F_STRING(HostName), 
	arg2 	:= F_STRING(level), 
	arg3 	:= F_STRING(message),
	sOut 	=> sPayloadPub,
	bError 	=> bError, 
	nErrID 	=> nErrID 
);

published := fbMqttClient.Publish(	
	sTopic			:= sTopicLog, 
	pPayload		:= ADR(sPayloadPub), 
	nPayloadSize	:= LEN2(ADR(sPayloadPub)),
	eQoS			:= TcIotMqttQos.AtMostOnceDelivery,
	bRetain			:= FALSE, 
	bQueue			:= FALSE 
);
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Comm_MQTT_Influx">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_Comm_MQTT_Influx._handleMQTTMessage">
      <LineId Id="78" Count="2" />
      <LineId Id="159" Count="0" />
      <LineId Id="82" Count="0" />
      <LineId Id="160" Count="0" />
      <LineId Id="86" Count="2" />
      <LineId Id="158" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="128" Count="4" />
      <LineId Id="92" Count="0" />
      <LineId Id="135" Count="3" />
      <LineId Id="134" Count="0" />
      <LineId Id="94" Count="0" />
      <LineId Id="139" Count="3" />
      <LineId Id="95" Count="5" />
      <LineId Id="143" Count="0" />
      <LineId Id="103" Count="0" />
      <LineId Id="125" Count="0" />
      <LineId Id="105" Count="6" />
      <LineId Id="126" Count="0" />
      <LineId Id="170" Count="11" />
      <LineId Id="113" Count="0" />
      <LineId Id="127" Count="0" />
      <LineId Id="115" Count="0" />
      <LineId Id="144" Count="0" />
      <LineId Id="117" Count="5" />
      <LineId Id="133" Count="0" />
      <LineId Id="123" Count="1" />
      <LineId Id="61" Count="0" />
    </LineIds>
    <LineIds Name="FB_Comm_MQTT_Influx.Publish">
      <LineId Id="19" Count="1" />
      <LineId Id="40" Count="0" />
      <LineId Id="21" Count="4" />
      <LineId Id="27" Count="2" />
      <LineId Id="41" Count="1" />
      <LineId Id="30" Count="9" />
      <LineId Id="43" Count="1" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Comm_MQTT_Influx.PublishLog">
      <LineId Id="20" Count="1" />
      <LineId Id="37" Count="0" />
      <LineId Id="22" Count="2" />
      <LineId Id="26" Count="2" />
      <LineId Id="38" Count="0" />
      <LineId Id="29" Count="7" />
      <LineId Id="39" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>