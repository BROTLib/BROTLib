<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_AltAzTelescopeControl" Id="{ab7ff9f6-f0dc-473a-bd67-87367819c272}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_AltAzTelescopeControl EXTENDS FB_BaseTelescopeControl IMPLEMENTS I_AltAzTelescope
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	eq2hor				: FB_EQ2HOR;
	hor2eq				: FB_HOR2EQ;
	
	fElevationCalc		: LREAL;	// elevation calulated when ra and dec are set by TCS 
	fAzimuthCalc		: LREAL;	// azimuth calulated when ra and dec are set by TCS
	fHourAngleCalc		: LREAL;	// hour angle calulated when ra and dec are set by TCS
	fDerotatorCalc		: LREAL;
	fRightAscensionCalc	: LREAL;	// right ascension calculated from current Alt/Az
	fDeclinationCalc	: LREAL;	// declination calculated from current Alt/Az
	
	fElevationOffset	: LREAL := 0.0;		// absolute offset for elevation
	fAzimuthOffset 		: LREAL := 0.0;		// absolute offset for azimuth
	fDerotatorOffset	: LREAL := 0.0;		// offset for the de-rotator
	
	fDerotatorVelocity	: LREAL;	// rotational velocity of the derotator
	fElevationVelocity	: LREAL;	// rotational velocity of the elevation
	fAzimuthVelocity	: LREAL;	// rotational velocity of the elevation
	
	fElevationPointing  	: LREAL;	// true position of the elevation with applied pointing model
	fAzimuthPointing    	: LREAL;	// true position of the azimuth with applied pointing model
	fRightAscensionPointing	: LREAL;	// RA from pointing Az/El
	fDeclinationPointing	: LREAL;	// Dec from pointing Az/El
	
	fDerotationSign	: LREAL := 1.0;	// sign of derotation
END_VAR
VAR PERSISTENT
	EOFF				: LREAL;	// elevation encoder zero offset
	AN_E				: LREAL;	// azimuth axis offset/misalignment north-south
	AE_E				: LREAL;	// azimuth axis offset/misalignment east-west
	TF					: LREAL;	// gravitational flexure correction at the horizon
	AOFF				: LREAL;	// azimuth encoder zero offset
	BNP					: LREAL;	// collimation error of the electromagnetic axis
	AN_A				: LREAL;	// azimuth axis offset/misalignment north-south
	AE_A				: LREAL;	// azimuth axis offset/misalignment east-west,
	NPAE				: LREAL;	// Non-perpendicularity between the mount azimuth and elevation axes
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// get the system time
fbTime();
fJD := DateTime2JD(fbTime.time_RTCEX2);
fLST := CT2LST(telescopeConfig.longitude, fJD);

// RA/Dec to Alt/Az/HA
eq2hor(
	ra := fRightAscension,
	dec := fDeclination,
	lon := telescopeConfig.longitude,
	lat := telescopeConfig.latitude,
	JD := fJD,
	alt => fElevationCalc,
	az => fAzimuthCalc,
	ha => fHourAngleCalc);
	
// apply pointing model
fElevationCalc := fElevationCalc + PointingModelElevation(fAzimuthCalc, fElevationCalc, EOFF + fElevationOffset, AN_E, AE_E, TF);
fAzimuthCalc := fAzimuthCalc + PointingModelAzimuth(fAzimuthCalc, fElevationCalc, AOFF + fAzimuthOffset, BNP, AN_A, AE_A, NPAE);

// calculate derotator position 
fDerotatorCalc := F_DerotatorPosition2(fAzimuthCalc, fElevationCalc, fDeclinationCalc, telescopeConfig.latitude, fDerotationSign) + fDerotatorOffset;

// calculate velocities
fDerotatorVelocity := F_Derotatorvelocity(fElevationCalc, fAzimuthCalc, telescopeConfig.latitude);
fElevationVelocity := F_Elevationvelocity(fAzimuthCalc, telescopeConfig.latitude);
fAzimuthVelocity := F_Azimuthvelocity(fElevationCalc, fAzimuthCalc, telescopeConfig.latitude);

// clip azimuth to limits
IF (fAzimuthCalc > 310.0 AND fAzimuthVelocity > 0.0) OR (fAzimuthCalc > 440.0) THEN
	fAzimuthCalc := fAzimuthCalc - 360.0;
ELSIF (fAzimuthCalc < 80.0 AND fAzimuthVelocity < 0.0) OR (fAzimuthCalc < -50.0) THEN
	fAzimuthCalc := fAzimuthCalc + 360.0;
END_IF

// clip derotator to limits
IF (fDerotatorCalc > 290.0 AND fDerotatorVelocity > 0.0) OR (fDerotatorCalc > 380.0) THEN
	fDerotatorCalc := fDerotatorCalc - 360.0;
ELSIF (fDerotatorCalc < 20.0 AND fDerotatorVelocity < 0.0) OR (fDerotatorCalc < -70.0) THEN
	fDerotatorCalc := fDerotatorCalc + 360.0;
END_IF

// Run the Base telescope and the state machine
SUPER^();
// apply pointing model
fElevationPointing := fElevationCurrent - PointingModelElevation(fAzimuthCurrent, fElevationCurrent, EOFF, AN_E, AE_E, TF);
fAzimuthPointing := fAzimuthCurrent - PointingModelAzimuth(fAzimuthCurrent, fElevationCurrent, AOFF, BNP, AN_A, AE_A, NPAE);

// add offsets
fElevationCurrent := fElevationPointing - fElevationOffset;
fAzimuthCurrent := fAzimuthPointing - fAzimuthOffset;

// calculate equatorial coordinates
hor2eq(alt := fElevationCurrent, 
		az := fAzimuthCurrent,
		jd := fJD,
		lat := telescopeConfig.latitude,
		lon := telescopeConfig.longitude,
		altitude := telescopeConfig.altitude,
		ra => fRightAscensionCalc,
		dec => fDeclinationCalc);		
hor2eq(alt := fElevationPointing, 
		az := fAzimuthPointing,
		jd := fJD,
		lat := telescopeConfig.latitude,
		lon := telescopeConfig.longitude,
		altitude := telescopeConfig.altitude,
		ra => fRightAscensionPointing,
		dec => fDeclinationPointing);
]]></ST>
    </Implementation>
    <Method Name="_PublishTelemetry" Id="{313bc6a7-19ec-4722-9d6e-3ac84aa9588f}">
      <Declaration><![CDATA[METHOD _PublishTelemetry : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^._PublishTelemetry();

comm.Publish('telescope', 'dome', 'POSITION.INSTRUMENTAL.AZ.OFFSET', LREAL_TO_STRING(fAzimuthOffset));
comm.Publish('telescope', 'dome', 'POSITION.INSTRUMENTAL.ALT.OFFSET', LREAL_TO_STRING(fElevationOffset));	
]]></ST>
      </Implementation>
    </Method>
    <Property Name="AltitudeOffset" Id="{61d61b42-3772-48a4-bbf4-c8264f7b8459}">
      <Declaration><![CDATA[{warning 'add property implementation'}
PROPERTY AltitudeOffset : LREAL
]]></Declaration>
      <Get Name="Get" Id="{8a20eda7-7b14-4b00-8989-4e306a01fc43}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[AltitudeOffset := THIS^.fElevationOffset;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{1c47573f-0f89-4ecb-9bcf-d7511a1c4e19}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[THIS^.fElevationOffset := AltitudeOffset;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="AzimuthOffset" Id="{580c6b01-71eb-41a5-b2e8-f0166c9039a8}">
      <Declaration><![CDATA[{warning 'add property implementation'}
PROPERTY AzimuthOffset : LREAL
]]></Declaration>
      <Get Name="Get" Id="{ed413719-73e2-4660-85da-efe3c363e7f1}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[AzimuthOffset := THIS^.fAzimuthOffset;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{732d8f9f-01ec-4777-918f-f8df92d2605e}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[THIS^.fAzimuthOffset := AzimuthOffset;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="DerotatorOffset" Id="{da6437af-965e-40c2-8dea-4bf926c328a7}">
      <Declaration><![CDATA[{warning 'add property implementation'}
PROPERTY DerotatorOffset : LREAL
]]></Declaration>
      <Get Name="Get" Id="{340a3d36-9a30-422a-ad59-6bebfad8b2af}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[DerotatorOffset := THIS^.fDerotatorOffset;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{647130f3-7273-4691-ba3d-1e20d8a7157d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[THIS^.fDerotatorOffset := DerotatorOffset;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <LineIds Name="FB_AltAzTelescopeControl">
      <LineId Id="217" Count="3" />
      <LineId Id="175" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="17" Count="7" />
      <LineId Id="9" Count="0" />
      <LineId Id="43" Count="0" />
      <LineId Id="45" Count="2" />
      <LineId Id="84" Count="0" />
      <LineId Id="48" Count="1" />
      <LineId Id="85" Count="0" />
      <LineId Id="50" Count="3" />
      <LineId Id="130" Count="0" />
      <LineId Id="86" Count="5" />
      <LineId Id="129" Count="0" />
      <LineId Id="92" Count="5" />
      <LineId Id="222" Count="0" />
      <LineId Id="221" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="104" Count="23" />
      <LineId Id="44" Count="0" />
      <LineId Id="172" Count="0" />
    </LineIds>
    <LineIds Name="FB_AltAzTelescopeControl._PublishTelemetry">
      <LineId Id="5" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="6" Count="0" />
    </LineIds>
    <LineIds Name="FB_AltAzTelescopeControl.AltitudeOffset.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_AltAzTelescopeControl.AltitudeOffset.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_AltAzTelescopeControl.AzimuthOffset.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_AltAzTelescopeControl.AzimuthOffset.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_AltAzTelescopeControl.DerotatorOffset.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_AltAzTelescopeControl.DerotatorOffset.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>